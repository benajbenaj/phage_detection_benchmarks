(PROFILES,) = glob_wildcards(config["bracken_dir"] + "/{p}.txt")


rule all:
    input:
        expand(
            "{s}/{p}_{m}_R1.fastq",
            s=config["simulated_dir"],
            m=config["model"],
            p=PROFILES,
        ),
        expand(
            "{s}/{p}_{m}_R2.fastq",
            s=config["simulated_dir"],
            m=config["model"],
            p=PROFILES,
        ),


rule make_profiles:
    input:
        config["bracken_dir"] + "/{id}.txt",
    output:
        config["profiles_dir"] + "/{id}_profile.txt",
        config["profiles_dir"] + "/{id}_files.txt",
    params:
        bracken_profiler=config["bracken_profiler"],
        refseq=config["refseq_info"],
        out_dir=config["profiles_dir"],
        time=config["make_profiles_time"],
    threads: config["make_profiles_ntasks"],
    shell:
        """
        {params.bracken_profiler} \
            -t {params.refseq} \
            -o {params.out_dir} \
            {input}
        """


rule cat_genomes:
    input:
        config["profiles_dir"] + "/{id}_files.txt",
    output:
        config["profiles_dir"] + "/{id}_genomes.fasta",
    params:
        cat_genomes=config["cat_genomes"],
        out_dir=config["profiles_dir"],
        parent=config["refseq_dir"],
        time=config["cat_genomes_time"],
    threads: config["cat_genomes_ntasks"],
    shell:
        """
        {params.cat_genomes} \
            -p {params.parent} \
            -o {params.out_dir} \
            {input}
        """


rule simulate_reads:
    input:
        genomes=config["profiles_dir"] + "/{id}_genomes.fasta",
        abundances=config["profiles_dir"] + "/{id}_profile.txt",
    output:
        config["simulated_dir"] + "/{id}_{model}_R1.fastq",
        config["simulated_dir"] + "/{id}_{model}_R2.fastq",
    params:
        simulator=config["simulator"],
        env=config["iss_env"],
        reads=config["n_reads"],
        out_dir=config["simulated_dir"],
        time=config["simulate_reads_time"],
    threads: config["simulate_reads_ntasks"],
    shell:
        """
        source activate {params.env}
        {params.simulator} {params.reads} \
            --cpus {threads} \
            --model {wildcards.model} \
            --abundance_file {input.abundances} \
            --output {params.out_dir}/{wildcards.id}_{wildcards.model} \
            --genomes {input.genomes}
        """
