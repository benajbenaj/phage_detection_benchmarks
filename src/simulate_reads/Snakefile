(PROFILES,) = glob_wildcards(config["bracken_dir"] + "/{p}.txt")


rule all:
    input:
        expand(
            "{s}/{p}_{m}_R1.fastq",
            s=config["simulated_dir"],
            m=config["model"],
            p=PROFILES,
        ),
        expand(
            "{s}/{p}_{m}_R2.fastq",
            s=config["simulated_dir"],
            m=config["model"],
            p=PROFILES,
        ),


rule make_profiles:
    input:
        "{dir}/{id}.txt",
    output:
        "{dir}/{id}_profile.txt",
        "{dir}/{id}_files.txt",
    params:
        bracken_profiler=config["bracken_profiler"],
        refseq=config["refseq_info"],
        out_dir=config["profiles_dir"],
    shell:
        """
        {params.bracken_profiler} \
            -t {params.refseq} \
            -o {params.out_dir} \
            {input}
        """


rule cat_genomes:
    input:
        "{dir}/{id}_files.txt",
    output:
        "{dir}/{id}_genomes.fasta",
    params:
        cat_genomes=config["cat_genomes"],
        out_dir=config["profiles_dir"],
        parent=config["refseq_dir"],
    shell:
        """
        {params.cat_genomes} \
            -p {params.parent} \
            -o {params.out_dir} \
            {input}
        """


rule simulate_reads:
    input:
        genomes=config["profiles_dir"] + "/{id}_genomes.fasta",
        abundances=config["profiles_dir"] + "/{id}_profile.txt",
    output:
        "{out_dir}/{id}_{model}_R1.fastq",
        "{out_dir}/{id}_{model}_R2.fastq",
    params:
        simulator=config["simulator"],
        env=config["iss_env"],
        cpus=config["simulator_cpus"],
        reads=config["n_reads"],
    shell:
        """
        source activate {params.env}
        {params.simulator} {params.cpus} {params.reads} \
            --model {wildcards.model} \
            --abundance_file {input.abundances} \
            --output {wildcards.out_dir}/{wildcards.id}_{wildcards.model} \
            --genomes {input.genomes}
        """
